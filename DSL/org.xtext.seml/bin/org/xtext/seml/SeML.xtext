//grammar org.xtext.seml.SeML with org.eclipse.xtext.common.Terminals
grammar org.xtext.seml.SeML hidden(WS, ML_COMMENT, SL_COMMENT)

generate seML "http://www.xtext.org/seml/SeML"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	MainModel | ImportModel
;

MainModel:
	imports+=Import+ 
	('use' useCh+=[Characteristic|ALIAS] (',' useCh+=[Characteristic|ALIAS])*)?
	sentences+=Sentence*
;

ImportModel:
	staticIndividuals+=StaticIndividual+
	individualOptions+=FreeIndividual*
	characteristics+=Characteristic*
	objectProperties+=ObjectProperty*
;

Import:
	'import' name=STRING
;
	
Sentence:
	Relation | Assignment
;

Relation:
	ind1=[Individual|ALIAS] obj=[ObjectProperty|ALIAS] ind2+=[Individual|ALIAS] (',' ind2+=[Individual|ALIAS])*
;

//Only for individuals of type Property
Assignment:
	ind=[Individual|ALIAS] '=' literal=Value
;

Value:
	{FloatVal} 		val=FLOAT 
	| {BoolVal} 	val=BOOL 
	| {IntVal} 		val=INT 
	| {StringVal} 	val=STRING
;

Individual:			FreeIndividual | StaticIndividual;
FreeIndividual: 	'FreeIndividual' 	name=ALIAS iri=STRING; //Optional Individual references
ObjectProperty:		'ObjectProperty' 	name=ALIAS iri=STRING;
Characteristic:		'Characteristic' 	name=ALIAS iri=STRING;
StaticIndividual:	'StaticIndividual' 	name=ALIAS iri=STRING; //Model Properties and other initial components


/*
 * Terminal rules
 */
	
terminal FLOAT returns ecore::EDouble:
 	INT '.' ('0'..'9')+ (('e' | 'E') INT)?;

terminal BOOL returns ecore::EBoolean:
	'true' | 'false';

terminal INT returns ecore::EInt:
	'-'? ('0'..'9')+;

terminal STRING:
	'"' ('\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | '"'))* '"' | "'" ('\\' .
	/* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | "'"))* "'";

terminal ML_COMMENT:
	'/*'->'*/';
	
terminal SL_COMMENT:
	"//" !('\n' | '\r')* ('\r'? '\n')?;
	
//Begins with a non-number. Contains letters, numbers and (-*._)
terminal ALIAS:
	'-'? ('A'..'Z' | 'a'..'z' | '*' | '_' | '.') ('A'..'Z' | 'a'..'z' | '0'..'9' | '-' | '*' | '_' | '.')*;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

terminal ANY_OTHER:
	.;
